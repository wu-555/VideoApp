pluginManagement {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}
enableFeaturePreview("VERSION_CATALOGS")
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://www.jitpack.io' }
    }
    versionCatalogs {
        //create创造分组
        create('androidxLibs') {
            //library(别名,group,artifact).version(version)
            library('core-ktx', 'androidx.core', 'core-ktx').version('1.9.0')
            library('appcompat', 'androidx.appcompat', 'appcompat').version('1.6.1')
            library('constraintlayout', 'androidx.constraintlayout', 'constraintlayout').version('2.1.4')
            library('navigation-fragment', 'androidx.navigation', 'navigation-fragment-ktx').version('2.5.3')
            library('navigation-ui', 'androidx.navigation', 'navigation-ui-ktx').version('2.5.3')
            library('material', 'com.google.android.material', 'material').version('1.5.0')
            library('swiperefreshlayout', 'androidx.swiperefreshlayout', 'swiperefreshlayout').version('1.2.0-alpha01')
            library('palette', 'androidx.palette', 'palette').version('1.0.0')

            // paging3
            library('ktx-stdlib-common', 'org.jetbrains.kotlin', 'kotlin-stdlib-common').version('1.8.0')
            library('paging-runtime', 'androidx.paging', 'paging-runtime').version('3.1.1')

            //bundle(别名,[前面定义好的依赖别名])
            //androidX 通用依赖
            bundle('androidx', ['swiperefreshlayout', 'palette', 'core-ktx', 'appcompat', 'constraintlayout', 'navigation-fragment', 'navigation-ui', 'material', 'ktx-stdlib-common', 'paging-runtime'])

            // retrofit + okHttp3
            library('glide', 'com.github.bumptech.glide', 'glide').version('4.15.0')
            library('glide-compiler', 'com.github.bumptech.glide', 'compiler').version('4.15.0')
            library('glide-transformations', 'jp.wasabeef', 'glide-transformations').version('4.0.0')

            library('retrofit', 'com.squareup.retrofit2', 'retrofit').version('2.9.0')
            library('converter-gson', 'com.squareup.retrofit2', 'converter-gson').version('2.9.0')
            library('okhttp', 'com.squareup.okhttp3', 'okhttp').version('4.9.1')
            library('logging', 'com.squareup.okhttp3', 'logging-interceptor').version('4.9.1')
            bundle('http', ['retrofit', 'converter-gson', 'okhttp', 'logging', 'glide', 'glide-transformations'])

            // exoplayer
            library('exoplayer-core', 'com.google.android.exoplayer', 'exoplayer-core').version('2.18.5')
            library('exoplayer-ui', 'com.google.android.exoplayer', 'exoplayer-ui').version('2.18.5')
            bundle('exoplayer', ['exoplayer-core', 'exoplayer-ui'])

            // nav-plugin
            library('asm', 'org.ow2.asm', 'asm').version('9.2')
            library('asm-tree', 'org.ow2.asm', 'asm-tree').version('9.2')
            library('commons-io', 'commons-io', 'commons-io').version('2.6')
            library('kotlinpoet', 'com.squareup', 'kotlinpoet').version('1.2.0')
            library('agp', 'com.android.tools.build', 'gradle').version('7.4.1')

            bundle('nav-plugin', ['asm', 'asm-tree', 'commons-io', 'kotlinpoet', 'agp'])

            // qq_open_sdk
            library('qqopensdk', 'com.tencent.tauth', 'qqopensdk').version('3.52.0')

            // room
            def room_version = '2.5.0'
            library('room-runtime', 'androidx.room', 'room-runtime').version(room_version)
            library('room-ktx', 'androidx.room', 'room-ktx').version(room_version)
            library('room-compiler', 'androidx.room', 'room-compiler').version(room_version)
            bundle('room', ['room-runtime', 'room-ktx'])

            // camerax
            def camerax_version = "1.3.0-alpha04"
            library('camera-core', 'androidx.camera', 'camera-core').version(camerax_version)
            library('camera-camera2', 'androidx.camera', 'camera-camera2').version(camerax_version)
            library('camera-lifecycle', 'androidx.camera', 'camera-lifecycle').version(camerax_version)
            library('camera-view', 'androidx.camera', 'camera-view').version(camerax_version)
            library('camera-extensions', 'androidx.camera', 'camera-extensions').version(camerax_version)
            bundle('camerax', ['camera-core', 'camera-camera2', 'camera-lifecycle', 'camera-view', 'camera-extensions'])

            // gesture imageview
            library('photoView', 'com.github.chrisbanes', 'PhotoView').version('2.3.0')
        }
        create('googleLibs') {
            library('material', 'com.google.android', 'material').version('1.8.0')
        }
        create('bulidSdk') {
            //version(别名,版本号)
            version('compileSdk', '33')
            version('minSdk', '23')
            version('targetSdk', '33')
        }
        create('pluginLibs') {
            //plugin(别名,插件)
            plugin('hilt-android', 'com.google.dagger.hilt.android').version('2.41')
        }
        create('composeLibs'){
            library('bom','androidx.compose','compose-bom').version('2023.01.00')

            library('material', 'androidx.compose.material', 'material').withoutVersion()
            library('ui', 'androidx.compose.ui', 'ui').withoutVersion()
            library('tooling','androidx.compose.ui','ui-tooling').withoutVersion()
            library('preview', 'androidx.compose.ui', 'ui-tooling-preview').withoutVersion()
            library('icons', 'androidx.compose.material', 'material-icons-core').withoutVersion()
            library('icons-ext', 'androidx.compose.material', 'material-icons-extended').withoutVersion()
            // foundation使用了1.4.0-beta02，是因为该版本中LazyVerticalStaggeredGrid
            // 才支持自定义跨列(spanCount)的能力
            library('foundation', 'androidx.compose.foundation', 'foundation').version('1.4.0-beta02')
            library('activity-compose', 'androidx.activity', 'activity-compose').version('1.6.1')
            library('viewmodel-compose', 'androidx.lifecycle', 'lifecycle-viewmodel-compose').version('2.5.1')

            // 加载网络图片
            // 普通图片
            library('coil','io.coil-kt','coil-compose').version('2.4.0')
            // gif图片
            library('coil-gif','io.coil-kt','coil-gif').version('2.4.0')
            // svg图片
            library('coil-svg','io.coil-kt','coil-svg').version('2.4.0')
            bundle('compose', ['coil','coil-gif','coil-svg','material', 'ui', 'preview','tooling','icons', 'icons-ext','foundation','activity-compose','viewmodel-compose'])
        }
        create("myLibs") {
            from(files("gradle/libs.versions.toml"))
        }
    }
}

rootProject.name = "VideoApp"
include ':app'
include ':nav-plugin'
include ':nav-plugin-runtime'
